model { #MODEL 4
	#for use on ppv behavioral data
	#monks modeled hierarchically, collapsed across category
	#IMPORTANT: all units are in SECONDS of juice!!!!

	#initialize individual priors for each monk for each category
	for (ind in 1:nummonk){
		V[ind] ~ dnorm(0,1e2) #V[monk]
		sess.std[ind] ~ dunif(1e-6,1) #session-to-session variance in category values: same for all categories
		sess.prec[ind] <- pow(sess.std[ind],-2)
		logomega[ind] ~ dunif(-6,2)
		log(omega.std[ind]) <- logomega[ind] #standard deviation of "trial-to-trial" overdispersion for a given monk
		omega.prec[ind] <- pow(omega.std[ind],-2)
	}

	#initialize choice scale parameters -- one distribution for all monks
	#based on preliminary fit of distribution
	t.loc ~ dunif(0,0.5)
	t.scale ~ dunif(1e-4,0.1) 
	t.df ~ dunif(0.1,50)
	t.prec <- pow(t.scale,-2)

	#draw values for each category for each session, along with precisions for the t distribution
	for (ind in 1:numsess){
		pred.v[ind] <- V[sessvec[ind]]
		v[ind] ~ dnorm(pred.v[ind],sess.prec[sessvec[ind]]) #all draws are independent, with monk-specific sd
		resid.v[ind] <- v[ind]-pred.v[ind]

		choice.scale[ind] ~ dt(t.loc,t.prec,t.df)T(0,) #scale factor for dv scale
		choice.prec[ind] <- pow(choice.scale[ind],-1)
	}

	#loop over distinct decisions
	for (ind in 1:N){ 
		pred.lp[ind] <- choice.prec[session[ind]]*(dv[ind]+v[session[ind]]) #prediction for logit(p)
		linpred[ind] ~ dnorm(pred.lp[ind],omega.prec[sessvec[session[ind]]])
		resid.lp[ind] <- linpred[ind] - pred.lp[ind] #residual for logit(p)
		
		logit(p.img[ind]) <- linpred[ind]
		
		Nimg[ind] ~ dbin(p.img[ind],Ntot[ind])
		pred.N[ind] <- p.img[ind]*Ntot[ind]
		resid.N[ind] <- Nimg[ind]-pred.N[ind]
	}

	########## now draw some things we care about ##########

	for (ind in 1:nummonk){ #for each monk
		ppred.v[ind] ~ dnorm(V[ind],sess.prec[ind]) #generate a fake set of session pses for each monk
		ppred.choice.scale[ind] ~ dt(t.loc,t.prec,t.df)T(0,) #generate a session choice scale

		for (ind2 in 1:10) {
			ppred.resid.lp[ind,ind2] ~ dnorm(0,omega.prec[ind]) #generate several overdispersion residuals
		}
	}
	

	
	
}
